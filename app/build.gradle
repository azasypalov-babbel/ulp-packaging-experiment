plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'de.undercouch.download'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "0.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'de.babbel'
            //You can either define these here or get them from project conf elsewhere
            artifactId 'ulp'
            version "${android.defaultConfig.versionName}"
            // stage or dev
            artifact "$projectDir/aar/ulp_lib_${android.defaultConfig.versionName}.aar"
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url "https://maven.pkg.github.com/azasypalov-babbel/ulp-packaging-experiment/"
            credentials {
                username = ""
                password = ""
            }
        }
    }
}

task downloadZipFile(type: Download) {
    src 'https://github.com/azasypalov-babbel/ulp-packaging-experiment/raw/master/ulp_0.0.2.zip'
    dest new File(buildDir, '1.0.zip')
}

task downloadAndUnzipFile(dependsOn: downloadZipFile, type: Copy) {
    from zipTree(downloadZipFile.dest)
    into 'src/main/assets/universal-lesson-player'
}

task copyAAR(type: Copy) {
    from "$projectDir/build/outputs/aar/"
    include 'app-debug.aar'
    into "$projectDir/AAR/"
    rename 'app-debug.aar', "ulp_lib_${android.defaultConfig.versionName}.aar"
}

task prepareUniversalLessonPlayerAar(type: GradleBuild) {
    tasks = ['downloadAndUnzipFile', 'assemble', 'copyAAR', 'publish']
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}